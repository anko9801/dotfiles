{ pkgs, userConfig, ... }:

let
  inherit (userConfig) editor;
  inherit (userConfig.git) name email sshKey;

  # Global gitignore patterns
  globalIgnores = [
    # OS
    ".DS_Store"
    "Thumbs.db"
    "Desktop.ini"

    # Editors
    "*.swp"
    "*.swo"
    "*~"
    ".idea/"
    ".vscode/"
    "*.sublime-*"
    ".*.un~"

    # Build artifacts
    "node_modules/"
    "__pycache__/"
    "*.pyc"
    "*.pyo"
    ".pytest_cache/"
    "target/"
    "dist/"
    "build/"
    "*.egg-info/"

    # Environment
    ".env"
    ".env.local"
    ".env.*.local"
    ".envrc"
    ".direnv/"

    # Logs
    "*.log"
    "npm-debug.log*"
    "yarn-debug.log*"
    "yarn-error.log*"

    # Misc
    "*.bak"
    "*.tmp"
    "*.temp"
    ".cache/"
    ".worktrees/"
  ];

  # Git aliases
  aliases = {
    # Status/Log
    st = "status -sb";
    ds = "diff --staged";
    tree = "log --graph --all --format=format:'%C(bold blue)%h%C(reset) - %C(bold green)(%ar)%C(reset) %C(white)%s%C(reset) %C(dim white)- %an%C(reset)%C(bold yellow)%d%C(reset)' --abbrev-commit";
    root = "rev-parse --show-toplevel";

    # Branch
    sw = "switch";
    safe-switch = "!git stash push -m 'switch backup' && git stash apply && git switch \"$@\"";
    current = "rev-parse --abbrev-ref HEAD";
    remember = "!git diff $(git merge-base HEAD $(git symbolic-ref refs/remotes/origin/HEAD | sed 's@refs/remotes/origin/@@'))";
    prune = "!git fetch --prune && git branch -vv | grep ': gone]' | awk '{print $1}' | xargs -r git branch -d";

    # Push/Pull
    ps = "push";
    pl = "pull";
    please = "push --force-with-lease --force-if-includes";

    # Commit
    undo = "reset HEAD~1 --mixed";
    nevermind = "!git reset --hard HEAD && git clean -d -f";

    fixup = "commit --fixup HEAD";
    # absorb: auto-fixup + rebase, falls back to interactive on failure
    absorb = "absorb --and-rebase";
  };

  # Pre-commit hook for secret detection
  preCommitHook = ''
    #!/usr/bin/env bash
    set -euo pipefail
    if ! command -v gitleaks &>/dev/null; then
        echo "Warning: gitleaks not found. Install it to scan for secrets."
        exit 0
    fi
    gitleaks protect --staged --redact --exit-code 1
  '';
in
{
  home = {
    packages = with pkgs; [
      difftastic
      git-wt
      git-absorb
      onefetch
    ];

    file = {
      ".config/git/allowed_signers".text = "${email} ${sshKey}";

      # czg config (conventional commits with emoji)
      ".czrc".text = builtins.toJSON {
        useEmoji = true;
        emojiAlign = "center";
        types = [
          { value = "feat"; name = "feat:     ‚ú® A new feature"; emoji = "‚ú®"; }
          { value = "fix"; name = "fix:      üêõ A bug fix"; emoji = "üêõ"; }
          { value = "docs"; name = "docs:     üìù Documentation only changes"; emoji = "üìù"; }
          { value = "style"; name = "style:    üíÑ Code style (formatting, semicolons, etc)"; emoji = "üíÑ"; }
          { value = "refactor"; name = "refactor: ‚ôªÔ∏è  Code refactoring"; emoji = "‚ôªÔ∏è"; }
          { value = "perf"; name = "perf:     ‚ö°Ô∏è Performance improvements"; emoji = "‚ö°Ô∏è"; }
          { value = "test"; name = "test:     ‚úÖ Adding or updating tests"; emoji = "‚úÖ"; }
          { value = "build"; name = "build:    üì¶ Build system or dependencies"; emoji = "üì¶"; }
          { value = "ci"; name = "ci:       üé° CI/CD configuration"; emoji = "üé°"; }
          { value = "chore"; name = "chore:    üîß Other changes (tooling, etc)"; emoji = "üîß"; }
          { value = "revert"; name = "revert:   ‚è™ Revert a commit"; emoji = "‚è™"; }
        ];
        allowCustomScopes = true;
        allowEmptyScopes = true;
        allowBreakingChanges = [ "feat" "fix" ];
        upperCaseSubject = false;
        skipQuestions = [ "body" "footerPrefix" "footer" ];
      };

      ".config/git/hooks/pre-commit" = {
        executable = true;
        text = preCommitHook;
      };
    };
  };

  programs.git = {
    enable = true;
    lfs.enable = true;
    ignores = globalIgnores;

    signing = {
      key = sshKey;
      signByDefault = true;
    };

    settings = {
      user = { inherit name email; };
      gpg.ssh.allowedSignersFile = "~/.config/git/allowed_signers";
      alias = aliases;

      # Core
      core = {
        inherit editor;
        autocrlf = false;
        safecrlf = true;
        filemode = false;
        pager = "delta";
        hooksPath = "~/.config/git/hooks";
        quotepath = false;
        untrackedCache = true;
      };

      color.ui = "auto";
      column.ui = "auto";
      init.defaultBranch = "main";

      # Diff/Merge
      diff = {
        algorithm = "histogram";
        renames = true;
        colorMoved = "plain";
        mnemonicPrefix = true;
        external = "difft";
      };

      merge = {
        tool = "vimdiff";
        conflictstyle = "zdiff3";
      };

      interactive.diffFilter = "delta --color-only";

      # Pull/Push/Fetch
      pull = {
        ff = "only";
        rebase = true;
      };

      push = {
        autoSetupRemote = true;
        default = "current";
        followTags = true;
      };

      fetch = {
        prune = true;
        pruneTags = true;
        all = true;
        fsckobjects = true;
      };

      submodule.recurse = true;

      # Rebase
      rebase = {
        autostash = true;
        autosquash = true;
        updateRefs = true;
      };

      rerere = {
        enabled = true;
        autoupdate = true;
      };

      # Branch/Tag
      branch = {
        autosetupmerge = "always";
        autosetuprebase = "always";
        sort = "-committerdate";
      };

      tag = {
        gpgsign = true;
        sort = "version:refname";
      };

      # Misc
      wt.basedir = ".worktrees";
      blame.ignoreRevsFile = ".git-blame-ignore-revs";
      commit.verbose = true;
      help.autocorrect = "prompt";
      status.showUntrackedFiles = "all";
      log.date = "iso";
      feature.manyFiles = true;
      maintenance.auto = true;

      transfer.fsckobjects = true;
      receive.fsckObjects = true;

      url."ssh://git@github.com/".insteadOf = "https://github.com/";

      advice = {
        addIgnoredFile = false;
        statusHints = false;
        commitBeforeMerge = false;
        detachedHead = false;
      };
    };
  };

  programs.delta = {
    enable = true;
    options = {
      navigate = true;
      line-numbers = true;
      side-by-side = true;
      syntax-theme = "TwoDark";
      features = "decorations";
      hyperlinks = true;
      hyperlinks-file-link-format = "vscode://file/{path}:{line}";
      blame-palette = "#1e1e2e #313244 #45475a";
      decorations = {
        commit-decoration-style = "bold yellow box ul";
        file-style = "bold yellow ul";
        file-decoration-style = "none";
        hunk-header-decoration-style = "cyan box ul";
      };
    };
  };
}
